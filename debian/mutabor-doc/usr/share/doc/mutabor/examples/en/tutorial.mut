LOGIC
	equal KEY e = et12t [
		KEY m -> et12t 
		KEY i -> et12t2 
	]

"12 tone equal temperament (12tet)"
TONESYSTEM
	et12t = 69 [ A' ] half_tone 
TONE
	A' = 440 
INTERVAL
	half_tone = 12 ROOT 2 

"12 tone equal temperament (alternative definition)"
TONESYSTEM
	et12t2 = 69 [ A' ] half_tone2 
INTERVAL
	half_tone2 = 1/12 octave 
	octave = 2:1

LOGIC
	fifth KEY f = et12fifth [
		KEY j -> justfifth 
		KEY c -> circleoffifth 
		KEY p -> pythagoras 
	]

"12tet and just fifth"
TONESYSTEM
	et12fifth = 69 [ A' ] 7 half_tone 
	justfifth = 69 [ A' ] just_fifth 
INTERVAL
	just_fifth = 3:2 
TONE
	E' = A' + just_fifth - octave 
	B' = E' + just_fifth 
	Fis' = B' + just_fifth - octave 
	Cis' = Fis' + just_fifth - octave 
	Gis' = Cis' + just_fifth 
	D' = A' - just_fifth 
	G' = D' - just_fifth + octave 
	C' = G' - just_fifth 
	F' = C' - just_fifth + octave 
	Bes' = F' - just_fifth + octave 
	Es' = Bes' - just_fifth 
	As' = Es' - just_fifth + octave 
	" ... " 
"spiral of fifth an Pythagorean tuning"
TONESYSTEM
	circleoffifth = 63 [ Es', Bes', F', C',G',D',A',E',B',Fis',Cis',Gis' ] 12 just_fifth - 7 octave 
	pythagoras = 60 [ C', Cis', D', Es',E',F',Fis',G',Gis',A',Bes',B' ] octave 

"Mutable tuning: moving fifths." 
PATTERN 
	fifth = {0, *3, *4, 7,*9} 
RETUNING 
	transpo (distance) = @+distance []
LOGIC
	adaptive KEY a = pythagoras [
		SHIFTED fifth -> transpo(distance) 
	]
